bundle agent inventory_CVE_2014_6271
{
  meta:
      "description" string => "Remote exploit vulnerability in bash http://web.nvd.nist.gov/view/vuln/detail?vulnId=CVE-2014-6271";
      "tags" slist => { "autorun" };

  vars:
      "env" string => "$(paths.env)";
      "bash" string => "/bin/bash";
      # apparently bash is not part of paths body
      "echo" string => "$(paths.echo)";

    "test_result" string => execresult("$(env) x='() { :;}; $(echo) vulnerable' $(bash) -c 'echo testing CVE-2014-6271'", "useshell");

    CVE_2014_6271::
          "vulnerable"
           string => "CVE-2014-6271",
           meta => { "inventory", "attribute_name=Vulnerable CVE(s)" };

  classes:
      "CVE_2014_6271"
       expression => regcmp( "vulnerable.*", "$(test_result)" ),
       scope => "namespace",
       persistence => "10",
      comment => "We persist the class for 2 agent runs so that bundles
                  activated before this bundle can use the class on the next
                  agent execution to coordinate things like package updates.";

  reports:
      DEBUG|DEBUG_cve_2014_6217::
            "Test Result: $(test_result)";

    CVE_2014_6271.(inform_mode|verbose_mode)::
          "Tested Vulnerable for CVE-2014-6271: $($(this.bundle)_meta.description)";
          }